import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

plugins {
  id 'java'
  id 'idea'
}


java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.1'

    implementation 'com.microsoft.azure:azure-eventhubs:3.3.0'
    implementation 'com.microsoft.azure:qpid-proton-j-extensions:1.2.4'
    implementation 'com.microsoft.azure:azure-eventhubs-eph:3.3.0'
    implementation 'com.microsoft.azure:azure-storage:8.6.6'
    constraints {
        implementation 'com.fasterxml.jackson.core:jackson-core:2.16.2'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.2'
        implementation 'com.google.code.gson:gson:2.8.9'
        implementation 'com.google.guava:guava:32.0.1-jre'
        implementation 'commons-codec:commons-codec:1.14'
        implementation 'com.nimbusds:nimbus-jose-jwt:10.0.2'
        implementation 'com.nimbusds:oauth2-oidc-sdk:8.36.2'
        implementation 'com.squareup.okhttp3:okhttp:4.12.0'
        implementation 'com.squareup.okio:okio:3.6.0'
        implementation 'net.minidev:json-smart:2.4.9'
        implementation 'org.apache.commons:commons-lang3:3.18.0'
        implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.1.0'
    }
    compileOnly 'org.apache.logging.log4j:log4j-api:2.17.0' // provided by Logstash
    testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.0' // provided by Logstash
}

// Exclude adal4j and its transitive dependencies from runtimeClasspath
configurations.runtimeClasspath {
    exclude group: 'com.microsoft.azure', module: 'adal4j'
}

task generateGemJarRequiresFile {
    doLast {
        File jars_file = file('lib/logstash-input-azure_event_hubs.rb')
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtimeClasspath.incoming.artifacts.artifacts.each { artifact ->
                def id = artifact.id.componentIdentifier
                if (id instanceof org.gradle.api.artifacts.component.ModuleComponentIdentifier) {
                    w << "require_jar(\'${id.group}\', \'${id.module}\', \'${id.version}\')\n"
                }
            }
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        configurations.runtimeClasspath.incoming.artifacts.artifacts.each { artifact ->
            def id = artifact.id.componentIdentifier
            if (id instanceof org.gradle.api.artifacts.component.ModuleComponentIdentifier) {
                String group = id.group
                String name = id.module
                String version = id.version
                String groupPath = group.replaceAll('\\.', '/')
                File newJarFile = file("${vendorPathPrefix}/${groupPath}/${name}/${version}/${name}-${version}.jar")
                if (!newJarFile.parentFile.exists()) {
                    newJarFile.parentFile.mkdirs()
                }
                Files.copy(artifact.file.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
            }
        }
    }
}

vendor.dependsOn(generateGemJarRequiresFile)

